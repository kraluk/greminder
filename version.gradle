// Based on https://gist.github.com/tux2323/4262286

import java.time.LocalDate

class VersionClassPlugin implements Plugin<Project> {

    void apply(Project project) {

        project.getPlugins().apply(plugins.JavaPlugin)
        project.extensions.add("versionClass", VersionClassConfiguration)

        def javaGenSrc = new File(project.buildDir, "generated-sources")

        def makeVersionClassTask = project.task("makeVersionClass") << {
            def versionInfoPackagePath = "${project.versionClass.packageName}".replace('.', '/')
            def versionClassFile = new File(javaGenSrc, "${versionInfoPackagePath}/VersionInfo.java")
            versionClassFile.parentFile.mkdirs()
            versionClassFile.withWriter { content ->
                content.write(

/** Start VersionClass Template */
                        """
package ${project.versionClass.packageName};
/**
 * Version info class provides some build meta informations.
 */
public class VersionInfo {

   public static final String VERSION = "${project.versionClass.version}";

   public static final String NAME = "${project.name}";

   public static final String GROUP = "${project.group}";

   public static final String BUILD_DATE = "${LocalDate.now()}";
}
"""
/** End VersionClass Template */

                )
            }
        }
        // Inputs for the task
        makeVersionClassTask.inputs.property("project.version", project.version)
        makeVersionClassTask.inputs.property("project.name", project.name)
        makeVersionClassTask.inputs.property("project.group", project.group)
        // Outputs of the task
        makeVersionClassTask.outputs.dir javaGenSrc
        // Add the javaGenSrc folder as source set
        project.sourceSets {
            main {
                java {
                    srcDir "${javaGenSrc}"
                }
            }
        }
        // Update the task dependencies
        project.tasks.compileJava.dependsOn("makeVersionClass")
    }

}

class VersionClassConfiguration {
    String version = "${project.version}"
    String packageName = "${project.group}.version"
}